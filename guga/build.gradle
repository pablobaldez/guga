apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 24
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.0.0'
    compile 'com.android.support:design:24.0.0'

    // Rx framework
    compile 'io.reactivex:rxjava:1.1.5'
    // Rx for android schedulers
    compile 'io.reactivex:rxandroid:1.2.0'
    // Rx for lifecycle
    compile 'com.trello:rxlifecycle-navi:0.6.1'
    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding-recyclerview-v7:0.4.0'

    // to endless implementation
    compile 'com.vinaysshenoy:mugen:1.0.2'
}

apply from: 'bintray-upload.gradle'

group = 'com.pablobaldez'
def artifact = 'guga'
version = '0.0.6'

def siteUrl = 'https://github.com/pablobaldez/guga'
def gitUrl = 'https://github.com/pablobaldez/guga.git'
def issueUrl = 'https://github.com/pablobaldez/guga/issues'

bintray {
    user = project.ext.user
    key = project.ext.apiKey

    configurations = ['archives'] //When uploading configuration files
    pkg {
        repo = 'maven'
        name = artifact
        desc = 'Android mvp framework'
        websiteUrl = siteUrl
        issueTrackerUrl = issueUrl
        licenses = ['Apache-2.0']
        labels = ['aar', 'android', 'mvp', 'guga']
        publish = true
    }
}

install {
    repositories.mavenInstaller {
        pom {
            //noinspection GroovyAssignabilityCheck
            project {
                packaging 'aar'
                name artifact
                groupId group
                artifactId artifact
                version version
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'pablobaldez'
                        name 'Pablo Baldez'
                        email 'pablo_baldez@outlook.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}